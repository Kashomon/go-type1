input paramsa;

font_identifier:="gnos"; font_size 12pt#;
beginchar("+",gow#,goh#,0); "Empty vertex, center of board";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 pickup pensquare scaled smallpen;
 draw z4..z6;
 draw z2..z8;
 endchar;
beginchar("[",gow#,goh#,0); "Empty vertex, left edge of board";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=.98h; % Modified for thick lines
 y2=y5=y8=.5h;
 y3=y6=y9=.02h; % Modified for thick lines
 pickup pensquare scaled smallpen;
 draw z5..z8;
 pickup pensquare scaled .8pt;
 draw z4..z6;
 endchar;
beginchar("]",gow#,goh#,0); "Empty vertex, right edge of board";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=.98h; % Modified for thick lines
 y2=y5=y8=.5h;
 y3=y6=y9=.02h; % Modified for thick lines
 pickup pensquare scaled smallpen;
 draw z2..z5;
 pickup pensquare scaled .8pt;
 draw z4..z6;
 endchar;
beginchar("(",gow#,goh#,0); "Empty vertex, top edge of board";
 x1=x2=x3=.02w; % Modified for thick lines
 x4=x5=x6=.5w;
 x7=x8=x9=.98w; % Modified for thick lines
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 pickup pensquare scaled smallpen;
 draw z5..z6;
 pickup pensquare scaled .8pt;
 draw z2..z8;
 endchar;
beginchar(")",gow#,goh#,0); "Empty vertex, bottom edge of board";
 x1=x2=x3=.02w; % Modified for thick lines
 x4=x5=x6=.5w;
 x7=x8=x9=.98w; % Modified for thick lines
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 pickup pensquare scaled smallpen;
 draw z4..z5;
 pickup pensquare scaled .8pt;
 draw z2..z8;
 endchar;
beginchar("<",gow#,goh#,0); "Empty vertex, upper left corner";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=0.98w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0.02h;
 pickup pensquare scaled .8pt;
 draw z5..z6;
 draw z5..z8;
 endchar;
beginchar(">",gow#,goh#,0); "Empty vertex, upper right corner";
 x1=x2=x3=0.02h;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0.02h;
 pickup pensquare scaled .8pt;
 draw z2..z5;
 draw z6..z5;
 endchar;
beginchar(",",gow#,goh#,0); "Empty vertex, lower left corner";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=0.98w;
 y1=y4=y7=0.98h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 pickup pensquare scaled .8pt;
 draw z4..z5;
 draw z8..z5;
 endchar;
beginchar(".",gow#,goh#,0); "Empty vertex, lower right corner";
 x1=x2=x3=0.02w;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=0.98h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 pickup pensquare scaled .8pt;
 draw z4..z5;
 draw z2..z5;
 endchar;
beginchar("*",gow#,goh#,0); "Hoshi";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 pickup pensquare scaled smallpen;
 draw z4..z6;
 draw z2..z8;
 pickup pencircle scaled hoshi;
 drawdot z5;
 endchar;
beginchar("@",gow#,goh#,0); "Black Stone";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 z10=setback[z4,z5];
 z11=setback[z2,z5];
 z12=setback[z6,z5];
 z13=setback[z8,z5];
 pickup pencircle scaled whitepen;
 fill z10..z11..z12..z13..cycle;
 picture blackstone; blackstone:=currentpicture;
 endchar;
beginchar("!",gow#,goh#,0); "White Stone";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 z10=setback[z4,z5];
 z11=setback[z2,z5];
 z12=setback[z6,z5];
 z13=setback[z8,z5];
 pickup pencircle scaled whitepen;
 draw z10..z11..z12..z13..cycle;
 picture whitestone; whitestone:=currentpicture;
 endchar;
beginchar("1",gow#,goh#,0); "Circle";
 x1=x2=x3=.25w;
 x4=x5=x6=.5w;
 x7=x8=x9=.75w;
 y1=y4=y7=.75h;
 y2=y5=y8=.5h;
 y3=y6=y9=.25w;
 z10=setback[z4,z5];
 z11=setback[z2,z5];
 z12=setback[z6,z5];
 z13=setback[z8,z5];
 pickup pencircle scaled widepen;
 draw z10..z11..z12..z13..cycle;
 picture circle; circle:=currentpicture;
 endchar;
beginchar("C",gow#,goh#,0); "Black Stone, circled";
 currentpicture:=blackstone;
 x1=x2=x3=.25w;
 x4=x5=x6=.5w;
 x7=x8=x9=.75w;
 y1=y4=y7=.75h;
 y2=y5=y8=.5h;
 y3=y6=y9=.25w;
 z10=setback[z4,z5];
 z11=setback[z2,z5];
 z12=setback[z6,z5];
 z13=setback[z8,z5];
 pickup pencircle scaled 1.75widepen;
 undraw z10..z11..z12..z13..cycle;
 endchar;
beginchar("c",gow#,goh#,0); "White Stone, circled";
 currentpicture:=whitestone;
 addto currentpicture also circle;
 endchar;
beginchar("2",gow#,goh#,0); "Square";
 x5=.5w;
 y5=.5h;
 x10=x11=.18w; 
 x12=x13=.82w;
 y10=y12=.82h;
 y11=y13=.18h;
 z14=setback[z10,z5];
 z15=setback[z11,z5];
 z16=setback[z12,z5];
 z17=setback[z13,z5];
 pickup pensquare scaled widepen;
 draw z14--z15--z17--z16--cycle;
 picture square; square:=currentpicture;
 endchar;
beginchar("S",gow#,goh#,0); "Black Stone, squared";
 currentpicture:=blackstone;
 x5=.5w;
 y5=.5h;
 x10=x11=.18w; 
 x12=x13=.82w;
 y10=y12=.82h;
 y11=y13=.18h;
 z14=setback[z10,z5];
 z15=setback[z11,z5];
 z16=setback[z12,z5];
 z17=setback[z13,z5];
 pickup pensquare scaled 1.75widepen;
 undraw z14--z15--z17--z16--cycle;
 endchar;
beginchar("s",gow#,goh#,0); "White Stone, squared";
 currentpicture:=whitestone;
 addto currentpicture also square;
 endchar;
beginchar("3",gow#,goh#,0); "Triangle";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 x14=.066987w; 
 x15=.933012w;
 y14=y15=.25h;
 z10=setback[z4,z5];
 z16=setback[z14,z5];
 z17=setback[z15,z5];
 pickup pencircle scaled widepen;
 draw z10--z16--z17--cycle;
 endchar;
beginchar(":",gow#,goh#,0); "Black Stone, triangled";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 x14=.066987w; 
 x15=.933012w;
 y14=y15=.25h;
 edgecorrection=.07;
 z10=setback[z4,z5];
 z11=setback[z2,z5];
 z12=setback[z6,z5];
 z13=setback[z8,z5];
 z16=setback[z14,z5];
 z17=setback[z15,z5];
 z18=edgecorrection[z4,z5];
 z19=edgecorrection[z16,z5];
 z20=edgecorrection[z17,z5];
 fill z10..z11..z12..z13..cycle;
 pickup pencircle scaled 1.75whitepen;
 undraw z18--z19--z20--cycle;
 picture blackstonetriangled; blackstonetriangled:=currentpicture;
 endchar;
beginchar("T",gow#,goh#,0); "Black Stone, triangled";
 currentpicture:=blackstonetriangled;
 endchar;
beginchar(";",gow#,goh#,0); "White Stone, triangled";
 x1=x2=x3=0pt;
 x4=x5=x6=.5w;
 x7=x8=x9=w;
 y1=y4=y7=h;
 y2=y5=y8=.5h;
 y3=y6=y9=0;
 x14=.066987w; 
 x15=.933012w;
 y14=y15=.25h;
 z10=setback[z4,z5];
 z11=setback[z2,z5];
 z12=setback[z6,z5];
 z13=setback[z8,z5];
 z16=setback[z14,z5];
 z17=setback[z15,z5];
 pickup pencircle scaled whitepen;
 draw z10..z11..z12..z13..cycle;
 draw z10--z16--z17--cycle;
 picture whitestonetriangled; whitestonetriangled:=currentpicture;
 endchar;
beginchar("t",gow#,goh#,0); "White Stone, triangled";
 currentpicture:=whitestonetriangled;
 endchar;
beginchar("4",gow#,goh#,0); "X";
 x5=.5w;
 y5=.5h;
 x10=x11=.25w; 
 x12=x13=.75w;
 y10=y12=.75h;
 y11=y13=.25h;
 z14=setback[z10,z5];
 z15=setback[z11,z5];
 z16=setback[z12,z5];
 z17=setback[z13,z5];
 pickup pencircle scaled widepen;
 draw z14--z17;
 draw z15--z16;
 picture xx; xx:=currentpicture;
 endchar;
beginchar("X",gow#,goh#,0); "Black Stone, Xed";
 currentpicture:=blackstone;
 x5=.5w;
 y5=.5h;
 x10=x11=.25w; 
 x12=x13=.75w;
 y10=y12=.75h;
 y11=y13=.25h;
 z14=setback[z10,z5];
 z15=setback[z11,z5];
 z16=setback[z12,z5];
 z17=setback[z13,z5];
 pickup pencircle scaled 1.75widepen;
 undraw z14--z17;
 undraw z15--z16;
 endchar;
beginchar("x",gow#,goh#,0); "White Stone, Xed";
 currentpicture:=whitestone;
 addto currentpicture also xx;
 endchar;
beginchar("-",gow#,goh#,0); "Blob: draw area under label (use white ink)";
 x1=x2=x3=.25w;
 x4=x5=x6=.5w;
 x7=x8=x9=.75w;
 y1=y4=y7=.75h;
 y2=y5=y8=.5h;
 y3=y6=y9=.25h;
 z10=setback[z4,z5];
 z11=setback[z2,z5];
 z12=setback[z6,z5];
 z13=setback[z8,z5];
 pickup pencircle scaled whitepen;
 fill z10..z11..z12..z13..cycle;
 endchar;
end;
